// SPDX-FileCopyrightText: 2023 iteratec GmbH
//
// SPDX-License-Identifier: Apache-2.0

plugins {
    id 'java'
    id "io.freefair.lombok" version "6.5.1"
    id 'maven-publish'
    id 'jacoco'
}

group 'io.securecodebox'
version (System.getenv("DD_CLIENT_VERSION") ? System.getenv("DD_CLIENT_VERSION") : '1.0.0-SNAPSHOT')
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.springframework:spring-test:5.3.23'
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
    testImplementation 'org.mockito:mockito-core:3.+'

    implementation group: 'org.springframework', name: 'spring-web', version: '5.3.9'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.4'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.4'
}

publishing {
    publications {
        register("jar", MavenPublication) {
            from(components["java"])
            pom {
                name = 'DefectDojo Client Java'
                description = 'Java Client to interact with the DefectDojo API.'
                url = 'https://github.com/secureCodeBox/defectdojo-client-java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jannik.hollenbach'
                        name = 'Jannik Hollenbach'
                        email = 'jannik.hollenbach@iteratec.com'
                    }
                    developer {
                        id = 'robert.seedorff'
                        name = 'Robert Seedorff'
                        email = 'robert.seedorff@iteratec.com'
                    }
                    developer {
                        id = 'johannes.zahn'
                        name = 'Johannes Zahn'
                        email = 'johannes.zahn@iteratec.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:secureCodeBox/defectdojo-client-java.git'
                    developerConnection = 'scm:git@github.com:secureCodeBox/defectdojo-client-java.git'
                    url = 'https://github.com/secureCodeBox/defectdojo-client-java'
                }
            }
        }
    }
    repositories {
        repositories {
            maven {
                // change URLs to point to your repos, e.g. http://my.org/repo
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials{
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}
